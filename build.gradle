plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'com.raihanorium'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

node {
    download = false
    nodeProjectDir = file("${project.projectDir}/src/main/frontend")
}

def querydslVersion = "5.1.0"
def lombokVersion = '1.18.32'
def poiVersion = "5.2.5"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-validation"

    runtimeOnly 'org.postgresql:postgresql:42.7.3'
    runtimeOnly 'com.mysql:mysql-connector-j:8.3.0'
    annotationProcessor "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2.Final"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:3.1.0"

    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.apache.commons:commons-lang3:3.14.0'
    implementation "org.apache.poi:poi:${poiVersion}"
    implementation "org.apache.poi:poi-ooxml:${poiVersion}"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

tasks.named('npmInstall', NpmTask) {
    dependsOn 'npmSetup'
    args = ['install', '--force']
}

tasks.register('npmBuild', NpmTask) {
    dependsOn 'npmInstall'
    environment = ['REACT_APP_API_BASE_URL': '/rest/api/v1']
    args = ['run', 'build']
}

tasks.register('copyFrontendBuild', Copy) {
    dependsOn 'npmBuild'
    from "${project.projectDir}/src/main/frontend/build"
    into "${project.projectDir}/build/resources/main/static"
}

tasks.named('processResources') {
    dependsOn 'copyFrontendBuild'
}
